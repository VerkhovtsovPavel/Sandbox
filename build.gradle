import com.github.spotbugs.snom.SpotBugsPlugin
import com.github.spotbugs.snom.SpotBugsTask

buildscript {
    dependencies {
        classpath group: 'com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: libs.versions.spotbugsPlugin.get()
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'org.pavelverk.sandbox'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

test {
    useTestNG()
}

// apply plugin: 'checkstyle'
checkstyle {
    configDirectory = file("config/checkstyle")
    dependencies {
        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: libs.versions.checkstyle.get()
        checkstyle group: 'com.github.sevntu-checkstyle', name: 'sevntu-checks', version: libs.versions.sevntuChecks.get()
    }
}

// apply plugin: 'pmd'
pmd {
    toolVersion = libs.versions.pmd.get()
    ruleSets = []
}
pmdMain {
    ruleSetFiles = files("config/pmd/pmdMain-ruleset.xml")
}
tasks.withType(Pmd) {
    if (it.name.endsWith('Test')) {
        it.ruleSetFiles = files("config/pmd/pmdTest-ruleset.xml")
    }
}

apply plugin: SpotBugsPlugin

spotbugs.toolVersion = libs.versions.spotbugs.get()
tasks.withType(SpotBugsTask) {
    omitVisitors = ['LostLoggerDueToWeakReference',
                    'FindNullDeref', // https://github.com/spotbugs/spotbugs/issues/259
                    'FindReturnRef'
    ]
    if (it.name.endsWith('Test')) {
        omitVisitors.add('MethodReturnCheck')
    }
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: libs.versions.log4j.get()
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: libs.versions.log4j.get()
    implementation group: 'org.jetbrains', name: 'annotations', version: libs.versions.jetbrainsAnnotations.get()

    testImplementation group: 'org.testng', name: 'testng', version: libs.versions.testng.get()
}